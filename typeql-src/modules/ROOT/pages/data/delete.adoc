= Delete
:Summary: Deleting data from a TypeDB database.
:keywords: typeql, typedb, clause, delete, remove
:pageTitle: Delete

A Delete clause is used to delete data from a database.
You can delete a data instance, ownership of an attribute, or a role player reference of a relation.
For a practical guide on how to send a Delete query to TypeDB,
see the xref:{page-component-version}@manual::writing/delete.adoc[] page of the TypeDB Manual.

== Syntax

// tag::syntax[]
A `delete` clause starts with the `delete` keyword, followed by the pattern declaring data that is to be deleted:

[,typeql]
----
delete <delete_pattern>
----

where `<delete_pattern>` is a pattern of valid data-specific TypeQL statements including xref:statements/isa.adoc[] and
xref:statements/has.adoc[].

Every variable used in a delete clause must be bound by a preceding clause and present in an input Concept Map.
// end::syntax[]

[#_behavior]
== Behavior

// tag::delete_clause[]
[#_delete_clause]
A Delete clause deletes the data instances, ownerships, and role player assignments specified in the `delete` clause.
A Concept Map from input provides values/concepts for the variables in a `delete` clause.

To <<_deleting_data_instances,delete a data instance>>, you need to use an
xref:statements/isa.adoc[isa] statement in a `delete` clause.

To delete ownership of an attribute, use a xref:statements/has.adoc[has] statement.

Role players can be deleted with a
xref:statements/links.adoc[] statement.
See <<_deleting_role_players_from_a_relation>>.

=== Clause execution

[#_match_clause]
A `delete` clause is executed once per every Concept Map in its input.
Deleting data doesn't affect Concept Maps from input.

A Delete clause is idempotent: deleting the same concept twice will not result in any further changes to the database.

=== Clause response

A Delete clause returns a Promise of an empty response.
//#todo Double-check or reconsider!
// end::delete_clause[]

[#_deleting_data_instances]
== Delete data instances 

include::partial$iam-database-links.adoc[]

To delete an entity, attribute, or relation,
match it in a `match` clause, then in a `delete` clause,
use the matched variable in an `isa` statement with the type or supertype of the data instance.

Deleting an attribute will result in all owners of the attribute to lose ownership of it:

.Deleting an attribute
[,typeql]
----
match
$email "admin@jp.com" isa email;
delete
$email isa email;
----

When a data instance is deleted,
all assignments of roles played and attribute ownerships are also deleted.
The relations, where the roles were played, and attribute, that were owned,
stay in a database, unless deleted explicitly with an `isa` statement.

.Deleting an entity
[,typeql]
----
match
$p isa person, has email "john.parkson@gmail.com";
delete
$p isa person;
----

In the above query, `match` clause finds a `person` entity that has the `email` attribute with the value of
`john.parkson@gmail.com`.
Then, it removes the matched entity from a database.

Deleting a relation will result in all instances that played roles in the relation
to no longer play that role in the deleted relation.

.Deleting a relation
[,typeql]
----
match
$p isa person, has full-name "Pearle Goodman";
$pe (subject: $p) isa permission;
delete
$pe isa permission;
----

The above query does the following:

. Finds the `subject` entity (`$p`), with `full-name` attribute value of `Pearle Goodman`.
. Finds all `permission` relations (`$pe`) relating the `subject` entity `$p` (as the `subject` role).
. Deletes all matched permissions `$pe`.

In short, it removes all permissions for `Pearle Goodman`.
Notice that we didn't specify all roles of a `permission` relation, e.g., the `object` role.

== Delete with type inheritance

include::partial$iam-database-links.adoc[]

A data instance can be deleted by using not only its exact type in a `delete` clause pattern,
but also any of its supertypes.
It can be especially useful in polymorphic queries.

.Deleting an attribute by its supertype
[,typeql]
----
match
$fn == "Bob";
delete
$fn isa attribute;
----

The above query matches and deletes all attributes with the value of `Bob`, regardless of their type.

== Delete ownerships

include::partial$iam-database-links.adoc[]

To delete an ownership of an attribute, match both the attribute and its owner and use a
xref:statements/has.adoc[] pattern in the `delete` clause of the same query:

.Deleting ownership
[,typeql]
----
match
$p has full-name $fn;
$fn == "Bob";
delete
$p has $fn;
----

The above query finds all data instances (`$p`) that have a `full-name` attribute (`$fn`)
whose value is equal to `Bob`.
For every matched pair of `$p` and `$fn` it deletes an ownership of `$fn` by `$p`.

[NOTE]
====
We are not using an xref:statements/isa.adoc[] statement in the `delete` clause above
because we don't want neither owner nor attribute to be deleted.
Only ownership of one by another.
====

[#_orphaned_attributes]
=== Orphaned attributes

include::partial$iam-database-links.adoc[]

If an attribute is not owned by any other data instance, then such an attribute is called orphaned.
Orphaned attributes are not deleted automatically, but quite often they are useless.
You can delete all orphaned attributes from a database with the following query:

.Deleting orphaned attributes
[,typeql]
----
match
$a isa attribute;
not {$x has $a;};
delete
$a isa attribute;
----

[#_deleting_role_players_from_a_relation]
== Delete role players

include::partial$iam-database-links.adoc[]

To delete a role player from a relation, match both the relation and data instance
that needs to stop playing a role in it, then use xref:statements/links.adoc[] statement in the
`delete` clause of the same query.
For example:

.Deleting a role player reference
[,typeql]
----
match
$p isa person, has full-name "Pearle Goodman";
$pe (subject: $p) isa permission;
delete
$pe ($p);
----

The above query does the following:

. Finds the `person` entity (`$p`), with the `full-name` attribute of the `Pearle Goodman` value.
. Finds all `permission` relations (`$pe`) relating the `$p` (as the `subject` role).
. From every matched relation `$pe` deletes the reference to the `$p`.

In short, it removes `Pearle Goodman` as the `subject` role player from all of its `permission` relations.
However, the relations are not deleted, and any other role player references are left intact.

With this query, an instance that plays the role in the relation is not removed,
but rather the information of that role player is removed from the relation.

We can omit the role in a pattern if its unambiguous, which role the data instance can play in the relation.

[NOTE]
====
Existence of a relation often implies the existence of all its role players.
Deleting a role player reference from a relation can create an incomplete data state.
Like a `marriage` relation with only one known spouse.
====

[WARNING]
====
Since relations must have at least one role player to exist,
all relations without a role player are implicitly deleted at committing.
====

[NOTE]
====
We are not using an xref:statements/isa.adoc[] statement in the `delete` clause
because we don't want any data instances to be deleted.
====

== Learn more

[cols-2]
--
.xref:{page-component-version}@learn::4-writing-data/4.3-deleting-data.adoc[]
[.clickable]
****
Learn more about deleting data from a database in the TypeDB Learning course.
****

.xref:{page-component-version}@manual::writing/delete.adoc[]
[.clickable]
****
See how to send a Delete query to TypeDB.
****
--
