= links

The `links` keyword is used in TypeQL data queries to specify players for roles in a relation.

== Syntax

The syntax of a `links` statement includes:

.Syntax
[,typeql]
----
<relation> "links (" <role> ":" <var> [ ,<role> ":" <var> ] ")" ;
----

where `<var>` is a variable to represent a data instance that plays the role in the relation.

This syntax can be combined with other statements, like xref:statements/isa.adoc[isa] and
xref:statements/has.adoc[has] as a
xref:statements/overview.adoc#_composite_statements[composite statement].

== Behavior

TypeQL statements with the `links` keyword can be used only in data queries.

A links statement can be used in a `match` clause to match existing relations or their role players.

If a links statement is used in an `insert`/`put` clause, it adds players for specified roles.

In a `delete` clause, a links statement is used to specify player to role associations to remove.
Deleting a player from a role can trigger a xref:annotations/cascade.adoc[cascade] delete.
However, the default behavior is not to delete the data instance that played a role or the relation,
but to remove the reference to the data instance as a player of the role.

== Usage with a match clause

For example, to find all users that have any permissions in a database,
match all permissions and find all players for the role `subject` in them:

.Links statement in a match clause example
[,typeql]
----
match
$p isa permission, links (subject:$user);
filter $user;
----

== Usage with an insert clause

For example, to insert a new permission relation for a given user to play the role `subject`
and a given file to play the role `object`:

.Links statement in an insert clause example
[,typeql]
----
match
$user isa person, has full-name $name;
$file isa file, has path $path;
insert
$p isa permission, links (subject:$user, object: $file);
----

== Usage with a delete clause

For example, to remove file as a role player for the `object` role in the relation we just added,
match it and use a links statement:

.Links statement in a delete clause example
[,typeql]
----
match
$user isa person, has full-name "Bob";
$file isa file, has path "README.md";
$p isa permission, links (subject:$user, object: $file);
delete
$p links (object: $file);
----

Make sure not to use an xref:statements/isa.adoc[isa statement]
in the `delete` clause to avoid deleting the whole relation.

The above example can fail to execute, because by default the `object` role has
xref:annotations/cardinality.adoc[cardinality] of exactly one,
so deleting a role player without providing a new one would result in cardinality constraint violation.
To fix that, set cardinality to `@card(0,1)` or insert a new player to the same role in the same query.

[#_usage_with_list]
== Usage with a list

include::partial$iam-database-links.adoc[]

A role of a relation can be defined as a list.
Then, instead of providing a single role player for a role, we can provide a list of roleplayers.

For example, the `permission` relation with the `object` role to be a list can be defined as follows:

.Defining a role for a list
[,typeql]
----
define
permission sub relation,
    relates object[],
    relates subject;
----

Notice the `[]` square brackets after the `object` role definition.
To use a role represented by a list in a data query, we also add the square brackets to its label:

.Using a links statement with a list for a role
[,typeql]
----
match
$p isa permission, links (object[]: $files);
filter $files;
----

== Learn more

[cols-2]
--
.xref:statements/relates.adoc[]
[.clickable]
****
Learn more about a `relates` statement in TypeQL.
****

.xref:statements/isa.adoc[]
[.clickable]
****
Learn more about an `isa` statement in TypeQL.
****
--

